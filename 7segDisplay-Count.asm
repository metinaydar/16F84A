;7segDisplay-Count.ASM--- 01.02.2005
;  PORTB YE BAGLI 7 SEGMENT DISPLAYIN KSS=3*M*N ESITLIGI ILE YAKLASIK HESAPLANAN
;DEGERDE MIKRO SANIYE BIRIMINDEN GECIKME ILE HEX TABANDAKI SAYILARIN GOSTERIMINDE
;CEVRIM TABLOSU KULLANARAK PORTA YA BAGLI BUTONLARA BASILDIKCA SAYIYI DEGISTIREN PRG.
;
;  NOT : PORTA NIN 0. BITI YANI RA0 (-) PULL UP YAPILMALI  >>  ARTIS
;  NOT : PORTA NIN 1. BITI YANI RA0 (-) PULL UP YAPILMALI  >>  EKSILME

	LIST	P=16F84A
	INCLUDE "P16F84A.INC"
	__CONFIG _CP_OFF &_WDT_OFF &_PWRTE_OFF &_XT_OSC

SAYAC1	EQU	h'0C'		;SAYAC1 FILE REGISTERININ RAM ADRESI
SAYAC2	EQU	h'0D'		;SAYAC2 FILE REGISTERININ RAM ADRESI
DEGER	EQU	h'0E'		;DEGER FILE REGISTERININ RAM ADRESI
	CLRF	PORTB		;PORTB ye bagli LED leri sondur
	BSF	STATUS, RP0	;BANK1 e gec
	CLRF	TRISB		;PORTB nin tum uclarini cikis yap
	MOVLW	h'FF'		;PORTA YA ATAMAK ICIN W YA FF ATA
	MOVWF	TRISA		;PORTA NIN TUM PINLERINI GIRIS YAP
	BCF	STATUS, RP0	;BANK0 a gec
	MOVLW	h'0'		;ILK ONCE 0. ELEMAN
	MOVWF	DEGER		; CAGIRILACAK
	CALL	GOSTER
TEKRAR	
	BTFSC	PORTA,	0	;PORTA 0. BIT CLEAR ISE SONRAKI KOMUTU ATLA
	CALL	ARTIS		;
	BTFSC	PORTA,	1	;PORTA 1. BIT CLEAR ISE SONRAKI KOMUTU ATLA
	CALL	EKSILME		;

	GOTO	TEKRAR		;
ARTIS
	INCF	DEGER,	F	; DEGER = DEGER + 1 
	CALL	MASKELE
	CALL	GOSTER
	RETURN
EKSILME
	DECF	DEGER,	F	; DEGER = DEGER - 1 
	CALL	MASKELE
	CALL	GOSTER
	RETURN
GOSTER
	MOVF	DEGER,	W	;DEGER REG. W YA ATA
	CALL	TABLO		;W. ELEMANI CAGIR
	MOVWF	PORTB		;CAGRILAN ELEMANI GOSTER
	CALL	BEKLEME		; BUTONA BASILI TUTMA SURESINI BELIRLER
	RETURN
MASKELE
	MOVLW	b'00001111'	;MASKELEME ICIN W YA h'0F' ATA
	ANDWF	DEGER,	F	;DEGER REG. MASKELE, h'0F' GECMESINI ENGELLE
	RETURN
TABLO
	ADDWF	PCL,	F	; W REGISTERI ICINDEKI DEGERLE PROGRAM COUNTER LOW
				;ICINDEKI DEGERI TOPLA, PCL YE ATA
				;( PROGRAM AKISI YONLENMIS OLACAK )
	RETLW	h'3F'		;RETURN WITH LITERAL IN W REG.       '0'
	RETLW	h'06'		; '1'
	RETLW	h'5B'		; '2
	RETLW	h'4F'		; '3'
	RETLW	h'66'		; '4'
	RETLW	h'6D'		; '5'
	RETLW	h'7D'		; '6'
	RETLW	h'07'		; '7'
	RETLW	h'7F'		; '8'
	RETLW	h'6F'		; '9'
	RETLW	h'77'		; 'A'
	RETLW	h'7C'		; 'B'
	RETLW	h'39'		; 'C'
	RETLW	h'5E'		; 'D'
	RETLW	h'79'		; 'E'
	RETLW	h'71'		; 'F'
BEKLEME
	MOVLW	h'EF'		;SAYAC1 ( M ) ICERIGINI
	MOVWF	SAYAC1		;239 YAP
DONGU1
	MOVLW	h'FF'		;SAYAC1 ( M ) ICERIGINI
	MOVWF	SAYAC2		;255 YAP
DONGU2
	DECFSZ	SAYAC2,	F	;SAYAC2 NIN DEGERINI 1 AZALT, SONUCU YINE SAYAC2
	GOTO	DONGU2		;ICINDE SAKLI TUT VE "0" ISE 1 KOMUT ATLA
	DECFSZ	SAYAC1,	F	;SAYAC1 IN DEGERINI 1 AZALT, SONUCU YINE SAYAC1
	GOTO	DONGU1		;ICINDE SAKLI TUT VE "0" ISE 1 KOMUT ATLA
	RETURN
	END
